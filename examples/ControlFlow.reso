def main() -> i32:
    println("=== Control Flow in Reso ===")
    
    # Simple If Statement
    println("=== Simple If Statement ===")
    
    var temperature: i32 = 25
    
    if temperature > 30:
        println("It's hot outside!")
    
    if temperature < 10:
        println("It's cold outside!")
    
    if temperature >= 20 and temperature <= 30:
        println("Nice weather today!")
    
    # If-Else Statement
    println("=== If-Else Statement ===")
    
    var score: i32 = 85
    
    if score >= 60:
        println("You passed the exam!")
    else:
        println("You failed the exam.")
    
    # If-Else If-Else Chain
    println("=== If-Else If-Else Chain ===")
    
    var grade: i32 = 62
    
    if grade >= 90:
        println("Grade A - Excellent!")
    else if grade >= 80:
        println("Grade B - Good!")
    else if grade >= 70:
        println("Grade C - Average")
    else if grade >= 60:
        println("Grade D - Below Average")
    else:
        println("Grade F - Failed")
    
    # Nested If Statements
    println("=== Nested If Statements ===")
    
    var age: i32 = 25
    var hasLicense: bool = true
    var hasCar: bool = false
    
    if age >= 18:
        println("You are an adult")
        
        if hasLicense:
            println("You can legally drive")
            
            if hasCar:
                println("You can drive your own car")
            else:
                println("You need to borrow or rent a car")
        else:
            println("You need to get a driver's license")
    else:
        println("You are too young to drive")
    
    # Ternary Operator (if-else expression)
    println("=== Ternary Operator ===")
    
    var number: i32 = 15
    var parity = "even" if (number rem 2) == 0 else "odd"
    println("The number ".append(number.toString())
        .append(" is ")
        .append(parity))
    
    var maxValue = 100 if 100 > 50 else 50
    println("Maximum value: ".append(maxValue.toString()))
    
    # More complex ternary expressions
    var status = "positive" if number > 0 else ("zero" if number == 0 else "negative")
    println("Number status: ".append(status))
    
    # While Loop
    println("=== While Loop ===")
    
    var counter: i32 = 1
    println("Counting from 1 to 5:")
    
    while counter <= 5:
        println("Count: ".append(counter.toString()))
        counter += 1
    
    println("Counter after loop: ".append(counter.toString()))
    
    # Break Statement
    println("=== Break Statement ===")
    
    var searchTarget: i32 = 7
    var searchCounter: i32 = 1
    var found: bool = false
    
    println("Searching for ".append(searchTarget.toString()).append(":"))
    
    while searchCounter <= 10:
        println("Checking: ".append(searchCounter.toString()))
        
        if searchCounter == searchTarget:
            println("Found target!")
            found = true
            break  # Exit the loop immediately
        
        searchCounter += 1
    
    if not found:
        println("Target not found")
    
    # Continue Statement
    println("=== Continue Statement ===")
    
    println("Printing only odd numbers from 1 to 10:")
    
    var j: i32 = 0
    while j < 10:
        j += 1
        
        if (j rem 2) == 0:
            continue  # Skip the rest of the iteration for even numbers
        
        println("Odd number: ".append(j.toString()))